group 'org.jitsi.jitsi_meet_govar_flutter_sdk'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '2.1.0'
    ext {
        agp_version = '8.6.0'
    }
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$agp_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        maven {
            url "https://github.com/jitsi/jitsi-maven-repository/raw/master/releases"
        }
        google()
        mavenCentral()
        maven { url 'https://www.jitpack.io' }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

// Exclude media3-exoplayer-rtsp from all configurations to avoid duplicate classes
// The classes are already included in react-native-video AAR, so we exclude the separate dependency
configurations.all {
    exclude group: 'androidx.media3', module: 'media3-exoplayer-rtsp'
}

android {
    namespace 'org.jitsi.jitsi_meet_flutter_sdk'
    compileSdkVersion 36

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    defaultConfig {
        minSdkVersion 24
    }

    dependencies {
        testImplementation 'org.jetbrains.kotlin:kotlin-test'
        testImplementation 'org.mockito:mockito-core:5.0.0'
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation ('org.jitsi.react:jitsi-meet-sdk:12.0.0') { 
        transitive = true
        exclude group: 'androidx.media3', module: 'media3-exoplayer-rtsp'
    }
}
